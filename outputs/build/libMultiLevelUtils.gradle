ext['githubOwner'] = "ReleaseStandard"
ext['groupPathBase'] = "io.github.${githubOwner}.CodeEditor"

def p(msg) {
	def debug = System.env.DEBUG
	if ( debug ) {
		println msg
	}
}

// deduce the remaining part from an entry object
def getRemainingPart(dir) {
  def remainPart = ""
  def projectDirectory = getProjectPath(dir)
  def rootProjectDirectory = rootDir.getPath()
  remainPart = projectDirectory.replace(rootProjectDirectory,"")
  remainPart = remainPart.replaceAll("/",":")
  return remainPart
}

def getProjectPath(obj) {
  if ( obj instanceof Project ) {
    return obj.projectDir.getPath()
  }
  if ( obj instanceof File ) {
      return obj.getPath()
  }
  return obj	
}

// produce project path from the moduleString
def moduleToProject(moduleString) {
  def parts = moduleString.split(":")
  def group = parts[0]
  def module = parts[1]
  def version = parts[2]
  def remainPart = group.replace("${groupPathBase}","")
  remainPart = remainPart.replace(".",":")
  def path = remainPart + ":${module}"
  return path
}

/**
 * Relative include for subprojects
 */
def resolveRelativePath(path) {
    def remainPart = getRemainingPart(project)
    String[] parts = remainPart.split(":")
    String finalPath = ""
    for(int a = 0; a < parts.length-1; a=a+1) {
        if(parts[a].size() > 0 ) {
            finalPath += ":" + parts[a]
        }
    }
    finalPath += path
    def prj = project.findProject(finalPath)
    if ( prj != null ) {
        p "resolveRelativePath: finalPath=${finalPath},remainPart=${remainPart},path=${path}"
        return finalPath
    } else {
        String alternativePath = remainPart + path
        prj = project.findProject(alternativePath)
        if ( prj != null ) {
            p "resolveRelativePath: alternativePath=${alternativePath},path=${path}"
            return alternativePath
        }
    }
    throw new RuntimeException("project with path '${path}' not found")
}

/**
 * This extends project() function by making it relative.
 * @param path
 * @return
 */
def projectR(path,obj) {
    def resolvedPath = resolveRelativePath(path)
    def prj = null
    def v = "${version}"
    if ( obj == null ) {
        prj = project(resolvedPath)
    } else if ( obj instanceof Closure ) {
        prj = project(resolvedPath, obj)
    } else if ( obj instanceof String || obj instanceof GString ) {
        prj = project(resolvedPath)
        v = "${obj}"
    } else {
        println "projectR obj="+obj.getClass() + " not understood by projectR"
        System.exit(1)
    }
    p "projectR ${version}(resolved to ${v}) : resolve ${path}(${resolvedPath}) for ${project}"
    if ( v!= null ) {
        prj.setVersion("${v}")
        prj["version"] = "${v}"
    }
    return prj
}

ext.set("projectRA",{ String path, obj ->
    projectR(path, obj)
})
ext.set("projectR",{ String path ->
    projectR(path, null)
})