def p(msg) {
	def debug = System.env.DEBUG
	if ( debug ) {
		println msg
	}
}

// deduce the remaining part from an entry object
def getRemainingPart(dir) {
  def remainPart = ""
  def projectDirectory = getProjectPath(dir)
  def rootProjectDirectory = rootDir.getPath()
  remainPart = projectDirectory.replace(rootProjectDirectory,"")
  remainPart = remainPart.replaceAll("/",":")
  return remainPart
}

def getProjectPath(obj) {
  if ( obj instanceof Project ) {
    return obj.projectDir.getPath()
  }
  if ( obj instanceof File ) {
      return obj.getPath()
  }
  return obj	
}

// produce project path from the moduleString
def moduleToProject(moduleString) {
  def trgGroup = "io.github.ReleaseStandard.CodeEditor"
  def parts = moduleString.split(":")
  def group = parts[0]
  def module = parts[1]
  def version = parts[2]
  def remainPart = group.replace(trgGroup,"")
  remainPart = remainPart.replace(".",":")
  def path = remainPart + ":${module}"
  return path
}

//
// This class an universal way to embed dependencies into the final object
//

def universalEmbed(moduleSelector) {

    def resolvedSelector = moduleSelector
    if ( moduleSelector instanceof ProjectDependency ) {
        resolvedSelector = moduleSelector.getDependencyProject()
    }
    if ( moduleSelector instanceof String ) {
        resolvedSelector = rootProject.findProject(moduleToProject(moduleSelector))
    }

    def selector = moduleSelector
    if ( rootProject.hasProperty('buildFromLocal') ) {
        selector = resolvedSelector
    }

    if ( System.env.DEBUG ) {
        println "universalEmbed of ${moduleSelector} resolved to ${selector}"
    }

    if ( project.plugins.hasPlugin("com.android.library") ) {
        apply plugin: 'com.kezong.fat-aar'
        fataar {
            transitive = true
        }
        if ( selector instanceof Project ) {
            selector.afterEvaluate {
                if ( !selector.plugins.hasPlugin("com.android.library")) {
                    if ( System.env.DEBUG ) {
                        println "${project} -> ${selector} don't have the plugin"
                        println "Due to a limitation of fat-aar we must correct that here"
                    }
                    project.dependencies.api selector
                }
            }
        } 
        project.dependencies.embed selector
    } else if (project.plugins.hasPlugin("java-library")) {
        project.dependencies.api selector
    } else {
        println "[ERROR] ${selector} for ${project} is in an unknow form"
        for(Configuration c : project.configurations) {
            print "${c.name} "
        }
        println ""
    }
}



project.ext.set("universalEmbed",{ Object moduleSelector ->
        universalEmbed(moduleSelector)
})
