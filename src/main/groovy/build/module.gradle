//
// Used to compile a module from CodeEditor (eg editor)
//
buildscript {
        ext["buildScriptRepos"] = {
                flatDir {
                        dirs 'libs'
                }
                google()
                mavenCentral()
                jcenter() // Warning: this repository is going to shut down soon
                maven { url = 'https://jitpack.io' }
	        maven { url = 'https://s01.oss.sonatype.org/content/groups/public/' }
        }
        ext["buildScriptDep"] = {
                classpath "com.android.tools.build:gradle:4.2.1"
                classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
                classpath 'com.github.kezong:fat-aar:1.3.6'
                classpath 'org.netbeans.api:org-apache-tools-ant-module:RELEASE124'
        }

        repositories buildScriptRepos
        dependencies buildScriptDep

}

//
// Setup root project
//
ext['githubOwner'] = "ReleaseStandard"
group = "io.github.${githubOwner}.CodeEditor"
if ( version == "unspecified" ) {
        version = '1';
}

def usePlugin(project, plugin) {
	if ( System.env.USE_LOCAL_BUILDSRC ) {
                project.apply from: "${rootDir.getPath()}/buildSrc/outputs/build/${plugin}"
	} else {
		project.apply from: "https://raw.githubusercontent.com/${githubOwner}/CodeEditor-buildSrc/master/outputs/build/${plugin}"
	}
}
allprojects { Project p ->
        repositories buildScriptRepos
	usePlugin(p, "embed.gradle")
	usePlugin(p, "build.helper.gradle")
}

usePlugin(project, "packaging.gradle")
usePlugin(project, "newVersion.gradle")
usePlugin(project, "mavenCentralPublish.gradle")

