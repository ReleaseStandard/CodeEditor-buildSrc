// BEGIN embed.gradle
//
// Used to provide universalEmbed configuration property
//  - shadow for java library projects (jar)
//  - fat-aar for android library projects

configurations {
    universalEmbed
    implementation.extendsFrom(universalEmbed)
}

afterEvaluate {
    configurations.universalEmbed.collect {
        p("Add artifact ${it} to ${project}")
        if (plugins.hasPlugin("java-library")) {
            p("Need to add ${it} to ${project} a java-library(jar)")
            apply plugin: 'com.github.johnrengelman.shadow'
            for(Task t :tasks.withType(Jar)) {
                if ( t != shadowJar && t.getName().equals("jar") ) {
                    t.archiveClassifier.set('onlyView')
                    p("Add the dependency : ${t}(${t.getClass()}) -> shadowJar")
                    t.mustRunAfter shadowJar
                    t.dependsOn shadowJar
                }
            }
            shadowJar {
                archiveClassifier.set('')
            }
            configurations.shadow.extendsFrom(configurations.universalEmbed)
        } else if ( plugins.hasPlugin('com.android.library')) {
            p("Need to add ${it} to ${project} a com.android.library(aar)")
            apply plugin: 'com.kezong.fat-aar'
            fataar {
                transitive = true
            }
            configurations.embed.extendsFrom(configurations.universalEmbed)
        } else {
            p("Embed has failed : unknow form")
            System.exit(1)
        }
    }
}

// END
